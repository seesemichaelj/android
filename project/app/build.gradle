apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

def versionMajor = 1
def versionMinor = 1
def versionPatch = 7
def versionBuild = 1
ext.daggerVersion = '2.9'
ext.playLibVersion = '10.2.0'
ext.okHttpVersion = '3.4.1'
ext.supportLibVersion = '25.1.1'


android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "org.owntracks.android"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex : "org.owntracks.android.EventBusIndex"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
            minifyEnabled true
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            jniLibs.srcDir 'jniLibs'
            java.srcDirs = ['src/main/java', 'build/generated/source/greendao/org/owntracks/android/db']
        }
    }

    signingConfigs {
        debug {
            keyAlias 'debug.owntracks.org'
            keyPassword '__DEBUG__'
            storeFile file('./debug-keystore.jks')
            storePassword '__DEBUG__'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}
repositories {
    maven { url 'https://repo.eclipse.org/content/repositories/paho-snapshots/' }
}

dependencies {
    // Support libraries
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"

    // Play Services libraries
    compile "com.google.android.gms:play-services-gcm:${playLibVersion}"
    compile "com.google.android.gms:play-services-maps:${playLibVersion}"
    compile "com.google.android.gms:play-services-location:${playLibVersion}"
    compile "com.google.android.gms:play-services-places:${playLibVersion}"

    // Utility libraries
    compile 'org.greenrobot:eventbus-annotation-processor:3.0.1'
    compile 'com.google.guava:guava:19.0' // needed by Dagger
    compile "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.google.zxing:android-integration:3.2.1' // Code scanning
    compile 'org.greenrobot:greendao:3.2.0'
    compile 'org.altbeacon:android-beacon-library:2.9.2@aar'

    compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.1-20170210.121107-84'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'com.fasterxml.jackson.core:jackson-core:2.7.8'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.8'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile files('jniLibs/kalium-jni-1.0.0-SNAPSHOT.jar', )

    // Widget libraries
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.afollestad.material-dialogs:core:0.9.1.0'
    compile 'com.afollestad.material-dialogs:commons:0.9.1.0'
    compile('com.mikepenz:materialdrawer:5.8.0@aar') { transitive = true }
    compile 'com.mikepenz:materialize:0.8.8@aar'
}

greendao {
    schemaVersion 15
}

